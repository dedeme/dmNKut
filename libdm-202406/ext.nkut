// Copyright 09-Apr-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Functions using external programs.

do "import * as puppet from 'puppeteer'";

/// If 'isMozilla' is 'false', it calls "wget -q --no-cache -O - 'url'" and
/// returns the text read.
/// If 'isMozilla' is 'true', it calls:
///   wget --user-agent Mozilla
///     --load-cookies=/home/deme/.mozilla/firefox/bfrqeymk.default/cookies.sqlite
///     -q --no-cache -O - url
/// If the reading fails, it returns [error, false], otherwise returns
/// [html, true].
/// \s, b -> <promise>[s, b]
wget = \url, isMozilla -> {
  Moz = [
    "--user-agent", "Mozilla",
    "--load-cookies=/home/deme/" +
      ".mozilla/firefox/bfrqeymk.default/cookies.sqlite"
  ];
  Base = ["-q", "--no-cache", "-O", "-", url];
  Opts = isMozilla ? arr.concat([Moz, Base]) : Base;
  return sys.cmd("wget", Opts);
};

/// Calls 'bash' with a script for 'node.js' to read a web page.
/// If the reading fails, it returns [error, false], otherwise returns
/// [html, true].
///   url: Url to read.
///   tm : Milliseconds to wait for reading. A good value is 2000.
/// \s, i -> [s, b]
puppeteer = async \url, tm -> {
  browserV = [[]];
  try {
    browserV! = await (class puppet).launch({headless:'new'});
    page = await browserV!.newPage();
    page.setDefaultNavigationTimeout(tm);
    await page.goto(url, {waitUntil:'domcontentloaded'});
    await page.cookies();
    ct = await page.content();
    await browserV!.close();
    return [ct, true];
  } catch (e) {
    if (browserV! != []) await browserV!.close();
    return [e.message, false];
  }
};

/// Calls a kut program to read a web page.
/// If the reading fails, it returns [error, false], otherwise returns
/// [html, true].
///   url: Url to read.
///   tm : Seconds to wait for full page load. A good value is 2.
///        The time for wait until giving up is 30''.
/// \s, i -> [s, b]
wdriver = \url, tm -> {
  Rs = sys.cmd2("wdriver", ["page", url, math.toStr(tm)]);
  if (Rs! != "") return [Rs!, true];
  return ["Page " + url + " could not be read", false];
};

/// Returns the md5 sum of a file. It calls "md5sum 'f'".
///   f: File to check.
/// \s -> s
md5 = \f -> {
  r, ok = sys.cmd("md5sum", [f]);
  if (ok) return r[:32];
  throw r;
};

/// Returns the md5 sum of a string. It calls
///     bash -c "echo -n " + js.ws('s') + " | md5sum"
///   s: String to check.
/// \s -> s
md5Str = \s -> {
  r, ok = sys.cmd("bash", ["-c", "echo -n " + js.w(s) + " | md5sum"]);
  if (ok) return r[:32];
  throw r;
};

/// Returns the sha256 sum of a file. It calls "sha256sum 'f'".
///   f: File to check.
/// \s -> s
sha256 = \f -> {
  r, ok = sys.cmd("sha256sum", [f]);
  if (ok) return r[:64];
  throw r;
};

/// Returns the sha256 sum of a string. It calls
///     bash -c "echo -n " + js.ws('s') + " | sha256sum"
///   s: String to check.
/// \s -> s
sha256Str = \s -> {
  r, ok = sys.cmd("bash", ["-c", "echo -n " + js.w(s) + " | sha256sum"]);
  if (ok) return r[:64];
  throw r;
};

/// Converts 'Files' to 'pdf' format. It calls
///     If 'isBook' is true:
///     htmldoc --book --charset utf-8 --footer ... --size A4 -t pdf
///             --outfile 'fpdf' ['Files']
///     If 'isBook' is false:
///     htmldoc --webpage --charset utf-8 --footer ... --size A4 -t pdf
///             --outfile 'fpdf' ['Files']
/// htmldoc documentation: https://www.msweet.org/htmldoc/htmldoc.html
///   isBook: 'true' if a book shuld be generated.
///   Files : Files in HTML to convert.
///   fpdf  : Target file.
/// \b, [s...], s -> <bytes>
htmldoc = \isBook, Files, fpdf -> {
  R = sys.cmd2(
    "htmldoc",
    arr.concat([
      [ isBook ? "--book" : "--webpage",
      "--charset", "utf-8", "--footer", "...", "--size", "A4", "-t", "pdf",
      "--outfile", fpdf
      ], Files
    ])
  );
  if (R[1] != "" & !str.starts(str.trim(R[1]), "PAGES:")) throw R[1];
};

/// Converts 'html' to 'pdf' format. It calls 'htmldoc'.
///   isBook: 'true' if a book shuld be generated.
///   html  : Text in HTML to convert.
///   fpdf  : Target file.
/// \b, s, s -> <bytes>
htmldocStr = \isBook, html, fpdf -> {
  tmp = file.tmp("", "nkut_htmldoc");
  file.write(tmp, html);
  htmldoc(isBook, [tmp], fpdf);
  file.del(tmp);
};

