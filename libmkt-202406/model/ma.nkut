// Copyright 25-Jun-2024 ºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Mobil average.

import "../model";
import "doc/maDoc";

/// Returns a new 'appr' model.
/// \ -> <model>
mk = \ -> return model.mk(
    "MM",
    "Media móvil",
    maDoc.text,
    ["Días", "Banda"],
    [model.dayParam, model.percParam],
    refs
  );;

// \[[n.].], [n.] -> [[n.].]
refs = \:arr Closes, :arr Params -> {
  days = math.toInt(Params[0]);
  strip = Params[1];
  stripUp = 1 + strip;
  stripDown = 1 - strip;
  :arr Cls0 = Closes[0];
  ndates = Closes.size();
  ncos = Cls0.size();

  IsSolds = arr.mk(ncos, false);
  :arr Refs = Cls0.map(\cl -> return cl * stripDown;);
  Sums = arr.copy(Cls0);

  AllRefs = arr.mk(ndates, Refs.copy());
  for (idate = 1:ndates) {
    ClosesR = Closes[idate];
    OldCloses = idate >= days ? Closes[idate - days] : ClosesR;

    for (ico = 0:ncos) {
      c = ClosesR[ico];
      oldC = OldCloses[ico];
      rf = Refs[ico];
      Sums[ico] = Sums[ico] + c - (idate >= days ? oldC : 0.0);
      avg = Sums[ico] / (idate < days ? idate + 1 : days);
      if (IsSolds[ico]) {
        if (c > rf) {
          IsSolds[ico] = false;
          Refs[ico] = avg * stripDown;
        } else {
          newRf = avg * stripUp;
          if (newRf < rf) Refs[ico] = newRf;
          else Refs[ico] = rf;
        }
      } else {
        if (c < rf) {
          IsSolds[ico] = true;
          Refs[ico] = avg * stripUp;
        } else {
          newRf = avg * stripDown;
          if (newRf > rf) Refs[ico] = newRf;
          else Refs[ico] = rf;
        }
      }
    }
    AllRefs[idate] = Refs.copy();
  }
  return AllRefs;
};
