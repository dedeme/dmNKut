// Copyright 25-Jun-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Approximations model.

import "../model";
import "doc/apprDoc";

/// Returns a new 'appr' model.
/// \ -> <model>
mk = \ -> return model.mk(
    "APRX",
    "Aproximaciones sucesivas",
    apprDoc.text,
    ["Inicio", "Aprox."],
    [model.percParam, model.percParam],
    refs
  );;

// \[[n.].], [n.] -> [[n.].]
refs = \:arr Closes, :arr Params -> {
  start = Params[0];
  upStart = 1 + start;
  downStart = 1 - start;
  incr = Params[1];
  :arr Cls0 = Closes[0];
  ndates = Closes.size();
  ncos = Cls0.size();

  AllRefs = []; // [[n.].]
  IsSolds = arr.mk(ncos, false);
  Refs = []; // [n.]
  for (cl = Cls0) Refs.push(cl * downStart);

  AllRefs.push(Refs.copy());
  for (idate = 1:ndates) {
    ClosesR = Closes[idate];

    for (ico = 0:ncos) {
      c = ClosesR[ico];
      rf = Refs[ico];

      if (IsSolds[ico]) {
        if (c > rf) {
          IsSolds[ico] = false;
          Refs[ico] = c * downStart;
        } else {
          Refs[ico] = rf - (rf - c) * incr;
        }
      } else {
        if (c < rf) {
          IsSolds[ico] = true;
          Refs[ico] = c * upStart;
        } else {
          Refs[ico] = rf + (c - rf) * incr;
        }
      }
    }
    AllRefs.push(Refs.copy());
  }
  return AllRefs;
};
