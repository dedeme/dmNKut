// Copyright 25-Jun-2024 ºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Exponential mobil average.

import "../model";
import "doc/eaDoc";

/// Returns a new 'appr' model.
/// \ -> <model>
mk = \ -> return model.mk(
    "ME",
    "Media móvil exponencial",
    eaDoc.text,
    ["Días", "Banda"],
    [model.dayParam, model.percParam],
    refs
  );;

// \[[n.].], [n.] -> [[n.].]
refs = \:arr Closes, :arr Params -> {
  days = math.toInt(Params[0]);
  daysUp = days + 1;
  strip = Params[1];
  stripUp = 1 + strip;
  stripDown = 1 - strip;
  :arr Cls0 = Closes[0];
  ndates = Closes.size();
  ncos = Cls0.size();

  IsSolds = arr.mk(ncos, false);
  :arr Refs = Cls0.map( \cl -> return cl * stripDown;);
  Avgs = Cls0.copy();

  AllRefs = arr.mk(ndates, Refs.copy());
  for (idate = 1:ndates) {
    ClosesR = Closes[idate];

    for (ico = 0:ncos) {
      c = ClosesR[ico];
      rf = Refs[ico];
      avg = Avgs[ico];
      newAvg = idate < days
        ? avg + 2 * (c - avg) / (idate + 1)
        : avg + 2 * (c - avg) / daysUp
      ;
      Avgs[ico] = newAvg;

      if (IsSolds[ico]) {
        if (c > rf) {
          IsSolds[ico] = false;
          Refs[ico] = newAvg * stripDown;
        } else {
          newRf = newAvg * stripUp;
          if (newRf < rf) Refs[ico] = newRf;
          else Refs[ico] = rf;
        }
      } else {
        if (c < rf) {
          IsSolds[ico] = true;
          Refs[ico] = newAvg * stripUp;
        } else {
          newRf = newAvg * stripDown;
          if (newRf > rf) Refs[ico] = newRf;
          else Refs[ico] = rf;
        }
      }
    }
    AllRefs[idate] = Refs.copy();
  }
  return AllRefs;
};
