// Copyright 25-Jun-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// up-up, down-down (2) model.

import "../model";
import "doc/uudd0Doc";

/// Returns a new 'appr2' model.
/// \ -> <model>
mk = \ -> return model.mk(
    "SSBB0",
    "Sube-Sube/Baja-Baja (0)",
    uudd0Doc.text,
    ["Inicio"],
    [model.percParam],
    refs
  );;

// \[[n.].], [n.] -> [[n.].]
refs = \:arr Closes, :arr Params -> {
  start = Params[0];
  upStart = 1 + start;
  downStart = 1 - start;
  :arr Cls0 = Closes[0];
  ndates = Closes.size();
  ncos = Cls0.size();

  AllRefs = []; // [[n.].]
  IsSolds = arr.mk(ncos, false);
  Refs = []; // [n.]
  for (cl = Cls0) Refs.push(cl * downStart);

  AllRefs.push(Refs.copy());
  for (idate = 1:ndates) {
    ClosesR = Closes[idate];
    PrvCloses = Closes[idate - 1];

    for (ico = 0:ncos) {
      c = ClosesR[ico];
      prvC = PrvCloses[ico];
      rf = Refs[ico];

      if (IsSolds[ico]) {
        if (c > rf) {
          IsSolds[ico] = false;
          Refs[ico] = c * downStart;
        } else {
          dif = c - prvC;
          rf0 = rf * (prvC + dif) / prvC;
          rf2 = rf < rf0 ? rf : rf0;
          Refs[ico] = rf2;
        }
      } else {
        if (c < rf) {
          IsSolds[ico] = true;
          Refs[ico] = c * upStart;
        } else {
          dif = c - prvC;
          rf0 = rf * (prvC + dif) / prvC;
          rf2 = rf > rf0 ? rf : rf0;
          Refs[ico] = rf2;
        }
      }
    }
    AllRefs.push(Refs.copy());
  }
  return AllRefs;
};
