// Copyright 05-Aug-2024 ºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Daily file data from BYM.

import "readers/common";
import "data/fileQ";

/// \s -> {q: <fileQ>, Ws: [s.]}
read = async \code -> {
  url = "https://www.bolsasymercados.es/bme-exchange/es/" +
    "Mercados-y-Cotizaciones/Acciones/Mercado-Continuo/" +
    "Ficha/" + code
  ;
  :str html = await common.read(url, 2000);

  // \n, s -> [([n, n]|s), b]
  readValue = \ix, label -> {
    ix1a = html.indexFrom(label, ix);
    if (ix1a == -1) return(["Label '" + label + "' not found", false]);

    ix1b = html.indexFrom('">', ix1a + str.len(label));
    if (ix1b == -1) return (["Start field '" + label + "' not found", false]);

    ix2 = html.indexFrom("<", ix1b + 2);
    if (ix2 == -1) return (["End field '" + label + "' not found", false]);

    qOp = math.fromIso(html[ix1b + 2:ix2]);
    if (!qOp) return (["Bad number in '" + label + "'", false]);

    return [[ix2 + 1, qOp!], true];
  };

  Ws = [];

  open = -1;

  closeV = [-1];
  ixV = [0];
  IxClose, ok = readValue(ixV!, ">Último<");
  if (!ok) Ws.push(IxClose);
  else {
    ixV! = IxClose[0];
    closeV! = IxClose[1];
  }

  maxV = [-1];
  IxMax, ok2 = readValue(ixV!, ">Máximo<");
  if (!ok2) Ws.push(IxMax);
  else {
    ixV! = IxMax[0];
    maxV! = IxMax[1];
  }

  minV = [-1];
  IxMin, ok3 = readValue(ixV!, ">Mínimo<");
  if (!ok3) Ws.push(IxMin);
  else {
    ixV! = IxMin[0];
    minV! = IxMin[1];
  }

  volV = [-1];
  IxVol, ok4 = readValue(ixV!, ">Volumen<");
  if (!ok4) Ws.push(IxVol);
  else {
    ixV! = IxVol[0];
    volV! = IxVol[1];
  }

  return {q: fileQ.mk(open, closeV!, maxV!, minV!, volV!) , Ws};
};
